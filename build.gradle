apply plugin: 'groovy'
apply plugin: 'application'
apply plugin: 'idea'

configure(allprojects) {
    ext.groovy = '2.4.7'
    ext.slf4jVersion = '1.7.2'
    ext.logbackVersion = '1.1.7'
}

repositories {
    jcenter()
}

configurations {
    compile.exclude module: 'commons-logging'
}

dependencies {
    // Groovy
    compile "org.codehaus.groovy:groovy-all:${groovy}:indy"

    // Logging
    compile "org.slf4j:slf4j-api:$slf4jVersion"
    compile "ch.qos.logback:logback-classic:$logbackVersion"
    compile "org.slf4j:jcl-over-slf4j:$slf4jVersion"
//            'ch.qos.logback:logback-core:1.1.7'

    // Test
    testCompile 'junit:junit:4.12'

}

// Java options
sourceCompatibility = 1.8
targetCompatibility = 1.8
mainClassName = 'me.bdylanwalker.designpatterns.Main'

// Groovy options
[compileGroovy.groovyOptions, compileTestGroovy.groovyOptions]*.with {
    fork = true
    optimizationOptions = [ indy: true, 'int': false]
    encoding = 'UTF-8'
}

// Tasks
task wrap(type:Wrapper, description:"create a gradlew") {
    gradleVersion = '2.8'
}

task runApp(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath

}
